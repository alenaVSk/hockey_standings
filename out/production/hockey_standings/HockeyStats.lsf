MODULE HockeyStats;

REQUIRE System, Teams;

CLASS Game 'Игра';

date 'Дата' = DATA DATE (Game);
hostTeam 'Хозяева' = DATA Team (Game); // IN base NONULL ;
guestTeam 'Гости' = DATA Team (Game);
hostTeamName 'Хозяева' (Game game) = name(hostTeam(game));
guestTeamName 'Гости' (Game game) = name(guestTeam(game));

CLASS GamePlayer 'игроки';

game = DATA Game (GamePlayer);
team = DATA Team (GamePlayer);
teamName 'Команда игрока' (GamePlayer gp) = name(team(gp));
player = DATA Player (GamePlayer);
playerName 'имя игрока' (GamePlayer gp) = playerName(player(gp));
playerPosition 'Позиция' (GamePlayer gp) = position(player(gp));
goals 'голы' = DATA INTEGER (GamePlayer) NONULL;


CONSTRAINT hostTeam(Game team) = guestTeam(team) CHECKED BY hostTeam, guestTeam MESSAGE 'Хозяйская и гостевая команды должны быть разными';

CONSTRAINT (team(GamePlayer gp) != hostTeam(game(gp)) AND team(gp) != guestTeam(game(gp)))  
    CHECKED BY team[GamePlayer] MESSAGE 'Команда игрока должна быть либо хозяева, либо гости!';
CONSTRAINT teamName(GamePlayer gp) != teamTableName(player(gp)) CHECKED BY player[GamePlayer] MESSAGE 'получилось связать игрока';
//CONSTRAINT teamTable(player(GamePlayer gp)) != team(gp) CHECKED BY team[GamePlayer] MESSAGE 'Игрок не из выбранной команды!!!!!!'; //лишнее т.к. автозаполнение

hostGoals 'Х голы' = DATA INTEGER (Game);
guestGoals 'Г голы' = DATA INTEGER (Game);

CONSTRAINT hostGoals(Game game) = guestGoals(game) MESSAGE 'Игра не может закончиться вничью';

winner(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN hostTeam(game)
    ELSE guestTeam(game);

looser(Game game) = IF hostGoals(game) > guestGoals(game)
    THEN guestTeam(game)
    ELSE hostTeam(game);

hostGoalsTotal (Game g) = [ GROUP SUM goals(GamePlayer gp) BY game(gp), team(gp) ] (g, hostTeam(g)) MATERIALIZED;
CONSTRAINT hostGoalsTotal(Game g) != hostGoals(g)  MESSAGE 'Сумма голов хозяев не совпадает с введённым значением!';

guestGoalsTotal (Game g) = [ GROUP SUM goals(GamePlayer gp) BY game(gp), team(gp) ] (g, guestTeam(g)) MATERIALIZED;
CONSTRAINT guestGoalsTotal(Game g) != guestGoals(g) MESSAGE 'Сумма голов гостей не совпадает с введённым значением!';

CLASS GameResult 'Р/И' {
    win 'П',
    winOT 'ПО',
    winSO 'ПБ'
}

resultName 'Имя' (GameResult game) = staticCaption(game) IF game IS GameResult IN base;

userResult = DATA GameResult (Game);
result (Game game) = OVERRIDE userResult(game),
    (GameResult.win IF ((hostGoals(game) (-) guestGoals(game)) > 1 OR (guestGoals(game) (-) hostGoals(game)) > 1));
resultName 'Р/И' (Game game) = resultName(result(game));

CONSTRAINT ((hostGoals(Game game) (-) guestGoals(game)) > 1 OR (hostGoals(game) (-) guestGoals(game)) < -1) AND userResult(game)
    MESSAGE 'Результат игры определен автоматически';

CONSTRAINT ((hostGoals(Game game) (-) guestGoals(game)) < 2 AND (hostGoals(game) (-) guestGoals(game)) > -2) AND NOT userResult(game)
    MESSAGE 'Укажите результат игры';

hostGamesPlayed = GROUP SUM 1 BY hostTeam(Game game);
guestGamesPlayed = GROUP SUM 1 BY guestTeam(Game game);
gamesPlayed 'И' (Team team) = hostGamesPlayed(team) (+) guestGamesPlayed(team);

gamesWonBy(Team team, GameResult type) = OVERRIDE [GROUP SUM 1 BY winner(Game game), result(game)](team, type),
    0 IF team IS Team AND type IS GameResult MATERIALIZED;

gamesWon 'В' (Team team) = gamesWonBy(team, GameResult.win);
gamesWonOT 'ВО' (Team team) = gamesWonBy(team, GameResult.winOT);
gamesWonSO 'ВБ' (Team team) = gamesWonBy(team, GameResult.winSO);

gamesLostBy(Team team, GameResult type) = OVERRIDE [GROUP SUM 1 BY looser(Game game), result(game)](team, type),
    0 IF team IS Team AND type IS GameResult MATERIALIZED;

gamesLost 'П' (Team team) = gamesLostBy(team, GameResult.win);
gamesLostOT 'ПО' (Team team) = gamesLostBy(team, GameResult.winOT);
gamesLostSO 'ПБ' (Team team) = gamesLostBy(team, GameResult.winSO);

points 'Очки' (Team team) = gamesWon(team) * 3 + (gamesWonSO(team) + gamesWonOT(team)) * 2 + gamesLostOT(team) + gamesLostSO(team);

hostGoalsScored = GROUP SUM hostGoals(Game game) BY hostTeam(game);
guestGoalsScored = GROUP SUM guestGoals(Game game) BY guestTeam(game);
goalsScored 'Кол-во забитых голов' (Team team) = OVERRIDE hostGoalsScored(team) (+) guestGoalsScored(team), 0 IF team IS Team;

hostGoalsConceded = GROUP SUM guestGoals(Game game) BY hostTeam(game);
guestGoalsConceded = GROUP SUM hostGoals(Game game) BY guestTeam(game);
goalsConceded 'Кол-во пропущенных голов' (Team team) = OVERRIDE hostGoalsConceded(team) (+) guestGoalsConceded(team), 0 IF team IS Team;

place 'Место' (Team team) = PARTITION SUM 1 ORDER DESC points(team), gamesWon(team), gamesWonOT(team), gamesWonSO(team),
    (OVERRIDE goalsScored(team) (-) goalsConceded(team), 0), goalsScored(team);

totalGoals 'Всего голов' (Player p) = GROUP SUM goals(GamePlayer gp) BY player(gp);
place 'Место' (Player p) = PARTITION SUM 1 ORDER DESC totalGoals(p), playerName(p);

FORM teamAndPlayers 'Заполнение игр и игроков'
    
    OBJECTS game = Game PANEL
    PROPERTIES(game) date, hostTeamName, hostGoals, guestTeamName, guestGoals, resultName 
    PROPERTIES(game) NEW, EDIT, DELETE

    OBJECTS p = GamePlayer 
    PROPERTIES(p)  teamName, playerName, playerPosition , goals
    PROPERTIES(p) NEW, EDIT, DELETE
    FILTERS game(p) = game  
    
    EDIT Game OBJECT game
;

FORM scoreTable 'Турнирная таблица'
    OBJECTS game = Game
    PROPERTIES(game) READONLY date, hostTeamName, hostGoals, guestGoals, guestTeamName, resultName
    PROPERTIES(game) NEWSESSION NEW, EDIT, DELETE
    
    OBJECTS team = Team
    PROPERTIES(team) place, name, gamesPlayed, gamesWon, gamesWonOT, gamesWonSO,
        gamesLostSO, gamesLostOT, gamesLost, goalsScored, goalsConceded, points, NEW, DELETE
    ORDERS place(team)
    
;

FORM nameScoreTable 'Бомбардиры'
    OBJECTS p = Player
    PROPERTIES(p) place, playerName, teamTableName, position, totalGoals
    FILTERS totalGoals(p) > 0
    ORDERS place(p)
;

NAVIGATOR {
    NEW scoreTable;
    NEW nameScoreTable AFTER scoreTable;
}

